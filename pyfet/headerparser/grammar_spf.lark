//RFC 5322, RFC5321 and RFC 7208 with IPV6 support
header_spf:  ["Received-SPF:"][cfws] result FWS [comment FWS] [key_value_list]
header_return_path: ["Return-Path:"][FWS] reverse_path [CR LF]
header_received: ["Received:"] received_token* ";" date_time [CR LF]

//Received
received_token: word | angle_addr | addr_spec | domain | ipv6_address
word: quoted_string | atom
atom: [cfws] atext+ [cfws]
angle_addr:[cfws]"<" addr_spec ">" [cfws] | obs_angle_addr
addr_spec: local_part "@" domain
local_part: dot_atom | quoted_string | obs_local_part
obs_angle_addr: [cfws]"<" obs_route addr_spec">"[cfws]
obs_route: obs_domain_list ":"
obs_domain_list: (cfws | ",")* "@" domain ("," [cfws]["@" domain])*
obs_local_part: word ("." word)*

date_time: [day_of_week ","] date time [cfws]
day_of_week: ([FWS] DAYNAME) | obs_day_of_week
date: day MONTH year
time: time_of_day zone
DAYNAME: "Mon" | "Tue" | "Wed" | "Thu" | "Fri" | "Sat" | "Sun"
day: ([FWS] (DIGIT~1..2)+ FWS) | obs_day
MONTH: "Jan" | "Feb" | "Mar" | "Apr" | "May" | "Jun" | "Jul" | "Aug" | "Sep" | "Oct" | "Nov" | "Dec"
year: (FWS DIGIT~4 FWS) | obs_year
time_of_day: hour ":" minute [":" second]
zone: (FWS ("+" | "-") DIGIT~4 (DIGIT)* ) | OBSZONE
hour: DIGIT~2 | obs_hour
minute: DIGIT~2 | obs_minute
second: DIGIT~2 | obs_second
obs_day_of_week: [cfws] DAYNAME [cfws]
obs_day: [cfws] DIGIT~1..2 [cfws]
obs_year: [cfws] DIGIT~2 (DIGIT)* [cfws]
OBSZONE: "UT" | "GMT" | "EST" | "EDT" | "CST" | "CDT" | "MST" | "MDT" | "PST" | "PDT" | /[\x41-\x49\x4B-\x5A\x61-\x69\x6B-\x7A]/

obs_hour: [cfws] DIGIT~2 [cfws]
obs_minute: [cfws] DIGIT~2 [cfws]
obs_second: [cfws] DIGIT~2 [cfws]

//Return Path
reverse_path: path | "<>"
path: "<"[adl ":"] mailbox ">"
adl: at_domain ("," at_domain)*
at_domain: "@" domain
domain: subdomain ("." subdomain)*
subdomain: let_dig [ldh_str]
let_dig: ALPHA | DIGIT
ldh_str: (ALPHA | DIGIT | "-")* let_dig

mailbox: local_part_x "@" (domain | address_literal)
local_part_x: dot_atom_text | quoted_string_smtp
quoted_string_smtp: DQUOTE qcontent_smtp* DQUOTE
qcontent_smtp: QTEXTSMPT | QPAIRSMTP
QTEXTSMPT: /[\x20\x21\x23-\x5B\x5D-\x7E]/
QPAIRSMTP:/[\x20-\x7E]/
address_literal: "[" (ipv4_address | ipv6_address_literal | general_address_literal)"]"
ipv6_address_literal: "IPv6:" ipv6_address
general_address_literal: ldh_str ":" DCONTENT+
DCONTENT: /[\x21-\x5A\x5E-\x7E]/

//SPF
result : "pass" | "fail" | "softfail" | "neutral" | "none" | "temperror" | "permerror"
key_value_list : key_value_pair (";" [cfws] key_value_pair )* [";"]
key_value_pair : key [cfws] "=" (dot_atom | quoted_string | ipv6_address ) //added IPV6
key : "client-ip" | "envelope-from" | "helo" | "problem" | "receiver" | "identity" | "mechanism" | name
name : ALPHA (ALPHA | DIGIT | "-" | "_" | ".") 

dot_atom: [cfws] dot_atom_text [cfws]
dot_atom_text: atext+ ("." atext+)*
atext: ALPHA | DIGIT | /[!#$%&'*+-\/=?^_`|{}~]/

comment: "(" ([FWS]ccontent)* [FWS]")"
ccontent: ctext | quoted_pair | comment

ctext: /[\x20-\x7E&&[^\x28\x29\x5C]]/ | OBS_NO_WS_CTL

cfws: (([FWS]comment)* [FWS]) | FWS

FWS: ([(WSP)* CR LF] WSP)| WSP(CR LF WSP)

quoted_string: [cfws] DQUOTE ([FWS] qcontent)* [FWS] DQUOTE [cfws]
qcontent: QTEXT | quoted_pair
quoted_pair: ("\\" (VCHAR | WSP)) | obs_qp

QTEXT: /[!#-\[\]-~]/ | OBS_NO_WS_CTL
OBS_NO_WS_CTL: /[\x01-\x08\x0B\x0C\x0E-\x1F\x7F]/
DQUOTE: /"/
obs_qp: "\\" (/[\x00]/ | OBS_NO_WS_CTL | VCHAR)
VCHAR: /[\x21-\x7E]/

%import common.LETTER -> ALPHA
%import common.DIGIT -> DIGIT
%import common.CR -> CR
%import common.LF -> LF
%import common.WS_INLINE -> WSP

//IPV6 from regex101.com

ipv6_address: [cfws] (ip6_content | ip4_content) [cfws]
ip6_content: hex_b (":" hex_b) ~ 7 
    | "::" hex_b (":" hex_b) ~ 0..6
    | hex_b "::" hex_b (":" hex_b) ~ 0..5
    | hex_b ":" hex_b "::" hex_b (":" hex_b) ~ 0..4
    | hex_b (":" hex_b)~ 2  "::" hex_b (":" hex_b) ~ 0..3
    | hex_b (":" hex_b)~ 3  "::" hex_b (":" hex_b) ~ 0..2
    | hex_b (":" hex_b)~ 4  "::" hex_b (":" hex_b) ~ 0..1
    | hex_b (":" hex_b)~ 5  "::" hex_b
    | hex_b (":" hex_b)~ 6  "::" 

ip4_content: hex_b (":" hex_b) ~ 5 ipv4_address 
    | "::" hex_b (":" hex_b) ~ 0..4 ipv4_address
    | hex_b "::" hex_b (":" hex_b) ~ 0..3 ipv4_address
    | hex_b ":" hex_b "::" hex_b (":" hex_b) ~ 0..2 ipv4_address
    | hex_b (":" hex_b)~ 2  "::" hex_b (":" hex_b) ~ 0..1 ipv4_address
    | hex_b (":" hex_b)~ 3  "::" hex_b ipv4_address
    | hex_b (":" hex_b)~ 4  "::" ipv4_address


ipv4_address: IP4_BLOCK ("." IP4_BLOCK) ~ 3

hex_b: HEXDIGIT ~ 1..4
IP4_BLOCK: /[0-9][0-9]/ | /1 [0-9][0-9]/ | /2[0-4][0-9]/ | /25[0-5]/

HEXDIGIT: /[0-9a-fA-F]/